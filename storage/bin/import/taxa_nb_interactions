#!/usr/bin/env perl
use strict;
use warnings;
use DBI;
use Data::Dumper;

my $vinland = DBI->connect(
    sprintf('DBI:Pg:database=%s;host=%s', 'vinland_3', $ENV{'DB_HOSTNAME_DEV'}),
    $ENV{'DB_USERNAME_DEV'},
    $ENV{'DB_PASSWORD_DEV'},
);

my $select_taxa_sth = $vinland->prepare('
    SELECT taxon_id, left_value, right_value FROM taxa
');

my $select_nb_interactions_sth = $vinland->prepare('
    SELECT COUNT(DISTINCT e.interaction_id)
    FROM taxa AS t, proteins AS p, edges AS e
    WHERE p.id = e.source_id
    AND t.ncbi_taxon_id = p.ncbi_taxon_id
    AND t.left_value >= ? AND t.right_value <= ?
');

my $update_taxon_sth = $vinland->prepare('
    UPDATE taxa SET nb_interactions = ? WHERE taxon_id = ?
');

$select_taxa_sth->execute;

while (my $taxon = $select_taxa_sth->fetchrow_hashref) {
    $select_nb_interactions_sth->execute($taxon->{'left_value'}, $taxon->{'right_value'});
    my($nb_interactions) = $select_nb_interactions_sth->fetchrow_array;
    $select_nb_interactions_sth->finish;

    if ($nb_interactions > 0) {
        $update_taxon_sth->execute($nb_interactions, $taxon->{'taxon_id'});
    }
}

$select_taxa_sth->finish;

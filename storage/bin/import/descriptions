#!/usr/bin/env perl
use strict;
use warnings;
use DBI;
use Data::Dumper;
use Dotenv -load;

my $drakkar = DBI->connect(
    sprintf('DBI:Pg:database=%s;host=%s', $ENV{'DRAKKAR_DB_DATABASE'}, $ENV{'DRAKKAR_DB_HOSTNAME'}),
    $ENV{'DRAKKAR_DB_USERNAME'},
    $ENV{'DRAKKAR_DB_PASSWORD'},
);

my $vinland = DBI->connect(
    sprintf('DBI:Pg:database=%s;host=%s', $ENV{'VINLAND_DB_DATABASE'}, $ENV{'VINLAND_DB_HOSTNAME'}),
    $ENV{'VINLAND_DB_USERNAME'},
    $ENV{'VINLAND_DB_PASSWORD'},
);

my $select_descriptions_sth = $drakkar->prepare('
    SELECT
        a.pmid,
        m.psimi_id,
        p1.accession AS accession1, i1.name AS name1,
        p2.accession AS accession2, i2.name AS name2
    FROM
        methods AS m,
        associations AS a,
        descriptions AS d,
        interactors AS i1, proteins AS p1,
        interactors AS i2, proteins AS p2
    WHERE a.id = d.association_id
      AND m.id = d.method_id
      AND i1.id = d.interactor1_id
      AND i2.id = d.interactor2_id
      AND p1.id = i1.protein_id
      AND p2.id = i2.protein_id
      AND a.state = \'curated\'
      AND d.deleted_at IS NULL
    ORDER BY p1.accession ASC, p2.accession ASC
');

my $select_method_sth = $vinland->prepare('
    SELECT * FROM methods WHERE psimi_id = ?
');

my $select_protein_sth = $vinland->prepare('
    SELECT * FROM proteins WHERE accession = ? AND name = ?
');

my $select_interaction_sth = $vinland->prepare('
    SELECT * FROM interactions WHERE protein1_id = ? AND protein2_id = ?
');

my @descriptions = ();

$select_descriptions_sth->execute;

while (my $description = $select_descriptions_sth->fetchrow_hashref) {
    $select_method_sth->execute($description->{'psimi_id'});
    my $method = $select_method_sth->fetchrow_hashref;
    $select_method_sth->finish;

    die('method not found') if (! $method);

    $select_protein_sth->execute($description->{'accession1'}, $description->{'name1'});
    my $protein1 = $select_protein_sth->fetchrow_hashref;
    $select_protein_sth->finish;

    die(sprintf('protein not found %s - %s', $description->{'accession1'}, $description->{'name1'})) if (! $protein1);

    $select_protein_sth->execute($description->{'accession2'}, $description->{'name2'});
    my $protein2 = $select_protein_sth->fetchrow_hashref;
    $select_protein_sth->finish;

    die(sprintf('protein not found %s - %s', $description->{'accession2'}, $description->{'name2'})) if (! $protein2);

    my @sorted = sort {
        return +1 if ($a->{'type'} eq 'v');
        return -1 if ($b->{'type'} eq 'v');
        return $a->{'accession'} cmp $b->{'accession'};
    } ($protein1, $protein2);

    $select_interaction_sth->execute($sorted[0]->{'id'}, $sorted[1]->{'id'});
    my $interaction = $select_interaction_sth->fetchrow_hashref;
    $select_interaction_sth->finish;

    die(sprintf('interaction not found %s - %s', $sorted[0]->{'id'}, $sorted[1]->{'id'})) if (! $interaction);

    push(@descriptions, [
        $description->{'pmid'},
        $method->{'id'},
        $interaction->{'id'},
    ]);
}

$select_descriptions_sth->finish;

$vinland->do('COPY descriptions(pmid, method_id, interaction_id) FROM STDIN');

$vinland->pg_putcopydata(join("\t", @{$_}) . "\n") foreach (@descriptions);

$vinland->pg_putcopyend();
